apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 6
        versionName "2.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.google.code.gson:gson:2.8.2'
}

group = 'com.prof.youtubeparserpb'
version = '2.5'

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

bintray {
    user = 'prof18'
    //TODO: add api key
    key = 'API_KEY'
    pkg {
        repo = 'maven'
        name = 'YoutubeParser'

        version {
            name = '2.5'
            desc = 'A library to parse Youtube video'
            released = new Date()
            vcsTag = '2.5'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/praveenb/YoutubeParser.git'
        websiteUrl = 'https://github.com/praveenb/YoutubeParser'
    }
    configurations = ['archives']

}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}